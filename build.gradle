plugins {
  id "com.jfrog.bintray" version "1.8.5"
  id "java"
  id "checkstyle"
  id "com.github.spotbugs" version "5.0.13"
  id "org.sonarqube" version "4.0.0.2929"

  id "maven-publish"
  id "org.embulk.embulk-plugins" version "0.5.5"
}

repositories {
  mavenCentral()
}

group = "io.github.yuokada"
version = "0.6.0"
description = "Embulk plugin for generate dummy records by Java."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
}

dependencies {
  compileOnly("org.embulk:embulk-core:0.10.49") {
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
    exclude group: 'org.apache.commons', module: 'commons-lang3'
  }

  implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
  implementation 'org.apache.commons:commons-text:1.10.0'

  testImplementation "junit:junit:4.+"
}

embulkPlugin {
  mainClass = "io.github.yuokada.embulk.input.randomj.RandomjInputPlugin"
  category = "input"
  type = "randomj"
}

checkstyle {
  configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
  toolVersion = '6.14.1'
}
checkstyleMain {
  configFile = file("${project.rootDir}/config/checkstyle/default.xml")
  ignoreFailures = false
}
checkstyleTest {
  configFile = file("${project.rootDir}/config/checkstyle/default.xml")
  ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
  classpath = sourceSets.main.output + sourceSets.test.output
  source = sourceSets.main.allJava + sourceSets.test.allJava
}

spotbugs {
  ignoreFailures = true
  excludeFilter = file("spotbugs-exclude.xml")
}

// publishing {
//     publications {
//         embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
//             from components.java  // Must be "components.java". The dependency modification works only for it.
//         }
//     }
//     repositories {
//         maven {
//             url = "${project.buildDir}/mavenPublishLocal"
//         }
//     }
// }

gem {
  from("LICENSE.txt")
  authors = ["yuokada"]
  email = ["ykhrokada+rubygems@gmail.com"]
  summary = "Randomj input plugin for Embulk"
  homepage = "https://github.com/yuokada/embulk-input-randomj"
  licenses = ["MIT"]
  // metadata = [  // Optional -- if you need metadata in the gem.
  //     "foo": "bar"
  // ]
}

gemPush {
  host = "https://rubygems.org"
}
